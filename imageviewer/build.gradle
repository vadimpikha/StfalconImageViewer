apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: "com.jfrog.artifactory"
apply plugin: "maven-publish"

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        setProperty("archivesBaseName", "imageviewer")
    }
}

dependencies {
    api 'androidx.appcompat:appcompat:1.2.0'
    api 'androidx.transition:transition:1.3.1'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

repositories {
    maven { url "https://jitpack.io" }
    mavenCentral()
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId = "ua.tiras"
            artifactId = archivesBaseName
            version = "1.0.0-SNAPSHOT"
            artifact("$buildDir/outputs/aar/$archivesBaseName-release.aar")
            artifact(sourceJar)

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.api.allDependencies.each { dependency ->
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dependency.group)
                    dependencyNode.appendNode('artifactId', dependency.name)
                    dependencyNode.appendNode('version', dependency.version)
                }
            }
        }
    }
}

artifactory {
    contextUrl = artifactory_url
    publish {
        repository {
            repoKey = version.endsWith('SNAPSHOT') ? 'gradle-dev-local' : 'gradle-release-local'
            username = artifactory_username
            password = artifactory_password
        }
        defaults {
            publications('aar')
            publishArtifacts = true
            publishPom = true
        }
    }
}
